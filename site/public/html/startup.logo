*******************************************************************************
; File: screen.logo
;
;*******************************************************************************

define redraw []   [savepic :wwwaddr]
define clean  []   [setcolor 99 fillscreen setcolor 0]
define moveto [x y][%moveto  sx :x sy :y]
define lineto [x y][%lineto sx :x sy :y]
define draw   [x y][%lineto sx :x sy :y]

define dotdashedlineto   [x y][%dotdashedlineto sx :x sy :y]
define shortdashedlineto [x y][%shortdashedlineto sx :x sy :y]
define longdashedlineto  [x y][%longdashedlineto sx :x sy :y]
define dotshortdashedlineto  [x y][%dotshortdashedlineto sx :x sy :y]
define dotlongdashedlineto   [x y][%dotlongdashedlineto sx :x sy :y]
define shortlongdashedlineto [x y][%shortlongdashedlineto sx :x sy :y]


define %ldrawstring [s x y]  [%drawstring :s :x  + (stringw :s) / 2  :y]
define drawstring   [s x y]  [%drawstring :s sx :x sy :y]
define ldrawstring  [s x y]  [%ldrawstring :s sx :x sy :y]
define drawstringr [s x y r][%drawstringr :s sx :x sy :y :r]

; define sx [x][output 100 + (:x - :x0) / (:x1 - :x0) * (:xp1 - 200)]
; define sy [y][output 100 + (:y1 - :y) / (:y1 - :y0) * (:yp1 - 200)]

define sx [x][output :xpad + (:x - :x0) / (:x1 - :x0) * (:xp1 - (2 * :xpad) )]
define sy [y][output :ypad + (:y1 - :y) / (:y1 - :y0) * (:yp1 - (2 * :ypad) )]

to setup-screen
  make "wwwaddr "screen.png
;  newimage 800 int (800 * ( :vwid / :hwid )) ; set aspect ratio
;   newimage 800 600
   newimage :geo0   :geo1
  make "xpad 0.12 * :geo0
  make "ypad 0.17 * :geo1
  clean
  limits -1 1 -1 1
  lweight 3
  %setcolor $000000
  setfont "courier 16 1
  TickSize 10 20 10 20
end



to rel :x :y
  make "savex :x
  make "savey :y
  moveto :x :y
end

to limits :ix0 :ix1 :iy0 :iy1
  make "x0 :ix0 make "y0 :iy0
  make "x1 :ix1 make "y1 :iy1
  make "xp0 0
  make "xp1 :geo0
  make "yp0 0
  make "yp1 :geo1
end

to ylower
  output 10 * round ((yminimum - 5) / 10)
end

to yupper
  output 10 * round ((ymaximum + 5) / 10)
end

to xlower
  output 10 * round ((xminimum - 5) / 10)
end

to xupper
  output 10 * round ((xmaximum + 5) / 10)
end

to grid :n :m 
  let [xinc (:x1 - :x0) / :n
       yinc (:y1 - :y0) / :m]
  let [x :x0 y :y0]
  repeat (:x1 - :x0) / :xinc
    [make "x :x + :xinc
     moveto :x :y0 lineto :x :y1]
  repeat (:y1 - :y0) / :yinc
    [make "y :y + :yinc
     moveto :x0 :y lineto :x1 :y]
end

to xgrid :n 
  let [xinc (:x1 - :x0) / :n]
  let [x :x0 y :y0]
  repeat (:x1 - :x0) / :xinc
    [make "x :x + :xinc
     moveto :x :y0 lineto :x :y1]
end

to ygrid :n 
  let [yinc (:y1 - :y0) / :n]
  let [y :y0 x :x0]
  repeat (:y1 - :y0) / :yinc
    [make "y :y + :yinc
     moveto :y :x0 lineto :y :x1]
end

to labels
  let [xinc (:x1 - :x0) / 10
       yinc (:y1 - :y0) / 6]
  let [x :x0 y :y0]
  repeat (:x1 - :x0) / :xinc
    [%drawstring trim :x (sx :x + :xinc * .1) + (stringw trim :x) / 2 590
     make "x :x + :xinc]
  repeat (:y1 - :y0) / :yinc
    [%drawstring trim :y 790 - (stringw trim :y) / 2 (sy :y + :yinc * .1)
     make "y :y + :yinc]
end

to label :inStr
  let [labstr insertspace :inStr]
  drawstring :labstr :savex :savey
end

to xlabel :inStr
  let [xl insertspace :inStr] 
  drawstring :xl :x0 + 0.45 * (:x1 - :x0)      :y0 - 0.15 * (:y1 - :y0)
end

to ylabel :inStr
  let [yl insertspace :inStr]
  drawstringr :yl :x0 - 0.12 * (:x1 - :x0)	:y0 + 0.55 * (:y1 - :y0)   -90.0
end

to title :inStr
  let [tl insertspace :inStr] 
  drawstring :tl :x0 + 0.45 * (:x1 - :x0)      :y1 + 0.12 * (:y1 - :y0)
end

to insertunderscore :inStr
  let [outStr  "|| ]
  repeat count :inStr [
   ifelse ((first :inStr) = "| |) 
     [make "outStr word :outStr "|_| ]
     [make "outStr word :outStr first :instr]
   make "inStr  bf :inStr
  ]
  output :outStr
end

to insertspace :inStr
  let [outStr  "|| ]
  repeat count :inStr [
   ifelse ((first :inStr) = "|_|) 
     [make "outStr word :outStr "| | ]
     [make "outStr word :outStr first :instr]
   make "inStr  bf :inStr
  ]
  output :outStr
end

to tmlabel :unixTime :%x :%y  ;%x and %y = percentage of chart size (>100% ok)
  let [ nixTm (se :unixTime "unix)
        ppDateNow (dateformat "|y/M/d HH:mm:ss z| :unixTime "utc) ]

  ;label graph with Unix time & Stadard Date/Time
  drawstringr :nixTm     :x0 + :%x * (:x1 - :x0) :y0 + :%y * (:y1 - :y0) 90.0
  drawstringr :ppDateNow :x0 + (:%x - 0.05) * (:x1 - :x0) :y0 + :%y * (:y1 - :y0)  90.0
end

to xticks :n :m
  let [cnt 0]
  let [xoff 0]
  let [yoff :y0 - 0.05 * (:y1 - :y0)]
  repeat :n + 1 [
             make "xoff :x0 + (:cnt / :n) * (:x1 - :x0)
             drawstring (se (round (:m * :xoff) + 0.1) / :m) :xoff :yoff
             make "cnt :cnt + 1
            ]
end
 
to yticks :n :m
  let [cnt 0]
  let [yoff 0]
  let [xoff :x0 - 0.05 * (:x1 - :x0)]
  repeat :n + 1 [
             make "yoff :y0 + (:cnt / :n) * (:y1 - :y0)
             drawstring (se (round (:m * :yoff) + 0.1) / :m) :xoff :yoff
             make "cnt :cnt + 1
            ]
end 

to box
  ;setpensize 1
  moveto :x0 :y0
  lineto :x1 :y0
  lineto :x1 :y1
  lineto :x0 :y1
  lineto :x0 :y0
end
 
to trim :x
  output (word (int :x * 10) / 10)
end

to setcolor :c
  make "currentcolor nth remainder :c 200
  [$000000 $030303 $050505 $080808 $0A0A0A $0D0D0D $0F0F0F $121212 $141414
   $171717 $1A1A1A $1C1C1C $1F1F1F $212121 $242424 $262626 $292929 $2B2B2B
   $2E2E2E $303030 $333333 $363636 $383838 $3B3B3B $3D3D3D $404040 $424242
   $454545 $474747 $4A4A4A $4D4D4D $4F4F4F $525252 $545454 $575757 $595959
   $5C5C5C $5E5E5E $616161 $636363 $666666 $696969 $6B6B6B $6E6E6E $707070
   $737373 $757575 $787878 $7A7A7A $7D7D7D $808080 $828282 $858585 $878787
   $8A8A8A $8C8C8C $8F8F8F $919191 $949494 $969696 $999999 $9C9C9C $9E9E9E
   $A1A1A1 $A3A3A3 $A6A6A6 $A8A8A8 $ABABAB $ADADAD $B0B0B0 $B3B3B3 $B5B5B5
   $B8B8B8 $BABABA $BDBDBD $BFBFBF $C2C2C2 $C4C4C4 $C7C7C7 $C9C9C9 $CCCCCC
   $CFCFCF $D1D1D1 $D4D4D4 $D6D6D6 $D9D9D9 $DBDBDB $DEDEDE $E0E0E0 $E3E3E3 
   $E6E6E6 $E8E8E8 $EBEBEB $EDEDED $F0F0F0 $F2F2F2 $F5F5F5 $F7F7F7 $FAFAFA
   $FCFCFC $FF0000 $FF0D00 $FF1A00 $FF2600 $FF3300 $FF4000 $FF4D00 $FF5900
   $FF6600 $FF7300 $FF8000 $FF8C00 $FF9900 $FFA600 $FFB300 $FFBF00 $FFCC00
   $FFD900 $FFE600 $FFF200 $FFFF00 $E6FF00 $CCFF00 $B3FF00 $99FF00 $80FF00
   $66FF00 $4DFF00 $33FF00 $1AFF00 $00FF00 $00FF0D $00FF1A $00FF26 $00FF33
   $00FF40 $00FF4D $00FF59 $00FF66 $00FF73 $00FF80 $00FF8C $00FF99 $00FFA6
   $00FFB3 $00FFBF $00FFCC $00FFD9 $00FFE6 $00FFF2 $00FFFF $00F2FF $00E6FF
   $00D9FF $00CCFF $00BFFF $00B3FF $00A6FF $0099FF $008CFF $0080FF $0073FF
   $0066FF $0059FF $004DFF $0040FF $0033FF $0026FF $001AFF $000DFF $0000FF
   $0D00FF $1A00FF $2600FF $3300FF $4000FF $4D00FF $5900FF $6600FF $7300FF 
   $8000FF $8C00FF $9900FF $A600FF $B300FF $BF00FF $CC00FF $D900FF $E600FF
   $F200FF $FF00FF $FF00E6 $FF00CC $FF00B3 $FF0099 $FF0080 $FF0066 $FF004D
   $FF0033 $FF001A]
  %setcolor :currentcolor
end

to red
  color $FF0000
end

to green
  color $00FF00
end

to blue
  color $0000FF
end

to yellow
  color $FFFF00
end

to magenta
  color $FF00FF
end

to cyan
  color $00FFFF
end

to white
  color $FFFFFF
end

to black
  color $000000
end

to gray :decimal%
  if or (:decimal% > 1.0) (:decimal% < -1.0)
    [ print "|value for gray must be between -1 and 1|
      stop ]
  let [redvalue (lsh (logand $FF0000 :currentcolor) -16)
       greenvalue (lsh (logand $00FF00 :currentcolor) -8)
       bluevalue  (logand $0000FF :currentcolor)]

  let [ave (round ((:redvalue + :greenvalue + :bluevalue) / 3))]

  make "redvalue lsh (:redvalue + ((:ave - :redvalue) * :decimal%)) 16
  make "greenvalue lsh (:greenvalue + ((:ave - :greenvalue) * :decimal%)) 8
  make "bluevalue :bluevalue + ((:ave - :bluevalue) * :decimal%)

  color (:redvalue + :greenvalue + :bluevalue)
end

to darken :decimal%
  if or (:decimal% > 1.0) (:decimal% < -1.0)
    [ print "|The value for darken must be between -1 and 1|
      stop ]
  make "decimal% (1.0 - :decimal%)
  let [redvalue lsh (logand $FF0000 :currentcolor) -16
       greenvalue lsh (logand $00FF00 :currentcolor) -8
       bluevalue  logand $0000FF :currentcolor]

  make "redvalue lsh (:redvalue * :decimal%) 16
  make "greenvalue lsh (:greenvalue * :decimal%) 8
  make "bluevalue :bluevalue * :decimal%

  color (:redvalue + :greenvalue + :bluevalue)
end

to lighten :decimal%
  if or (:decimal% > 1.0) (:decimal% < -1.0)
    [ print "|The value for lighten must be between -1 and 1|
      stop ]
  let [redvalue lsh (logand $FF0000 :currentcolor) -16
       greenvalue lsh (logand $00FF00 :currentcolor) -8
       bluevalue  logand $0000FF :currentcolor]

  make "redvalue lsh (:redvalue + (($FF - :redvalue) * :decimal%)) 16
  make "greenvalue lsh (:greenvalue + (($FF - :greenvalue) * :decimal%)) 8
  make "bluevalue :bluevalue + (($FF - :bluevalue) * :decimal%)

  color (:redvalue + :greenvalue + :bluevalue)
end 

to color :incolor
 %setcolor :incolor
 make "currentcolor :incolor
end

e*******************************************************************************
; File: smplot.logo
;
;*******************************************************************************
;#############################################
;   DATA HANDLING
;#############################################
to save-xycol :fileName
  let [dx copylist :xcol-data
       dy copylist :ycol-data] 
  make "ncol 2
  blindexec word "|rm | :fileName
  prfopen :fileName
  prfprint :ncol
  prfprint se :xname :yname
  dotimes [i count :dx]
    [ prfprint se nth :i :dx nth :i :dy ]
  prfclose
  print se "|>> data saved to| :fileName
end

to save-data :fileName
  let [d copylist :data-list]
  blindexec word "|rm | :fileName
  wait 10
  prfopen :fileName
  prfprint :ncol
  dotimes [i count :d] 
    [prfprint nth :i :d ]
  prfclose
end

to restore-data :fileName
  make "data-list parse filetostring :fileName
  make "ncol nth 0 :data-list
  make "xname nth 1 :data-list
  make "yname nth 2 :data-list
  make "data-list bf bf bf :data-list
  xcol 1
  ycol 2
end

to select-data :n :m
  let [dx copylist :xcol-data
       dy copylist :ycol-data
       tdx []
       tdy []]
  dotimes [i count :dx]
    [ if and ((nth :i :dx) > (:n )) ((nth :i :dx) < (:m ))
        [ make "tdx (se :tdx (nth :i :dx))
          make "tdy (se :tdy (nth :i :dy)) ] ]
    make "xcol-data :tdx
    make "ycol-data :tdy
end

to select-all
  select-data -32766 32766
end

to show-select
  let [dx copylist :xcol-data
       dy copylist :ycol-data]
  dotimes [i count :dx]
    [ print se nth :i :dx nth :i :dy ]
end

to x-data :inStr :inList
  make "xname insertunderscore :inStr
  make "xcol-data :inList
end

to y-data :inStr :inList
  make "yname insertunderscore :inStr
  make "ycol-data :inList 
end

to data :m :namex :datax :namey :datay
  make "xname insertunderscore :namex
  make "yname insertunderscore :namey
  let [newlist []
       dx copylist :datax
       dy copylist :datay]
  make "newlist se :newlist :xname
  make "newlist se :newlist :yname
  make "newlist se :newlist :dx
  make "newlist se :newlist :dy
  make :m :newlist
end

to xcol :n
  let [ucol :n - 1]
  make "xcol-data []
  dotimes [i ((count :data-list) / :ncol)]
    [ make "xcol-data (se :xcol-data nth (:ucol + (:ncol * :i)) :data-list) ]
end

to ycol :n
  let [ucol :n - 1]
  make "ycol-data []
  dotimes [i ((count :data-list) / :ncol)]
    [ make "ycol-data (se :ycol-data nth (:ucol + (:ncol * :i)) :data-list) ]
end

to add-data :n :m
  make "data-list (se :data-list :n :m)
end

to init-data
  make "data-list []
  make "xcol-data []
  make "ycol-data []
end

to show-data
  let [dx copylist :xcol-data
       dy copylist :ycol-data]
  print (se :xname :yname) 
  dotimes [i count :dx]
    [ print (se nth :i :dx "|    | nth :i :dy) ]
end

;#############################################
;     STATISTICS
;#############################################
to average
  let [value 0
       sum 0
       avg 0
       cnt (count :ycol-data)]
  dotimes [i :cnt]
    [ make "sum  :sum + (nth :i :ycol-data) ]
  make "avg :sum / :cnt
  output :avg
end

to ymaximum
  output MaxEntry :ycol-data
end


to xmaximum
  output MaxEntry :xcol-data
end


to yminimum
  output MinEntry :ycol-data
end


to xminimum
  output MinEntry :xcol-data
end

to stddev
  let [std 0
       value 0
       sumsqr 0
       avg average
       var 0
       cnt count :ycol-data]
  dotimes [i :cnt] 
    [ make "value  ((nth :i :ycol-data) - :avg)
      make "sumsqr  :sumsqr + (:value * :value) ]
  make "std sqrt (:sumsqr / :cnt)
  output :std
end

to stddev-mean
  let [std 0
       value 0
       sumsqr 0
       avg average
       var 0
       cnt (count :ycol-data)
       std-mean 0]
  dotimes [i :cnt]
    [ make "value  ((nth :i :ycol-data) - :avg)
    make "sumsqr  :sumsqr + (:value * :value) ]

  make "std sqrt (:sumsqr / :cnt)
  make "std-mean :std / sqrt (:cnt)
  output :std-mean
end

to sort :n
  let [d copylist :n
       dc []
       s []
       i 0
       j 0
       value 0
       minval (nth 0 :d)
       cnt count :d]

  repeat :cnt [
    make "i 0
    make "cnt count :d
    make "minval 100000
    repeat :cnt [
      make "value nth :i :d   
      if (:value < :minval) [ 
        make "minval :value 
        make "j :i ]       
      make "i :i + 1 ] 
      make "i 0
      make "dc []
      repeat :cnt [      
        if or (:j < :i) (:j > :i)  [make "dc se :dc (nth :i :d)]               
        make "i :i + 1 ]
      make "s se :s :minval
      make "d :dc  
  ]
  output :s
end

to median
  let [d copylist :ycol-data
       cnt (count :d)
       dc sort :d
       val 0
       v1 0
       v2 0]
  ifelse ((:cnt % 2) > 0)
    [ make "val nth (:cnt / 2) :dc
      output :val ]
    [ make "v1 nth (:cnt / 2) :dc
      make "v2 nth (:cnt / 2 - 1) :dc
      output (:v1 + :v2) / 2 ]
end

to noise
  let [d copylist :ycol-data
       drestore copylist :ycol-data
       dc []
       val 0
       cnt (count :d)
       av-val average]

  repeat :cnt [
    make "val abs ((nth 0 :d) - :av-val)
    make "dc se :dc :val
    make "d bf :d ]
  make "ycol-data :dc
  setup-screen
  limits xlower xupper ylower yupper
  box
  xaxis 10 50 1 true
  yaxis 10 50 1 true
  title "|Residuals| 
  xlabel "|Time (seconds)| 
  ylabel "|res| 

;## setup the plotting style ##;
  ptype 2 
  points
  errorbars 0
  
;## Draw the plot ##;
  display-clear
  display-plot tile 1 1 1 1

;##restore the original ycol-data
  make "ycol-data copylist :drestore
end

to two-digit :n
   output (int ((:n * 100) + 0.5)) / 100
end

to four-digit :n
  output (int ((:n * 10000) + 0.5)) / 10000
end

to analyze
  print se "|dataset         | :yname
  print se "|N               | (count :ycol-data)
  print se "|minimum         | (four-digit yminimum)
  print se "|maximum         | (four-digit ymaximum)
  print se "|average         | (two-digit average)
  print se "|median          | (two-digit median)
  print se "|stddev          | (four-digit stddev)
  print se "|stddev/sqrt(N)  | (four-digit stddev-mean)
  print    "|mode             manually inspect list |
  print (se "|[|sort :ycol-data "|]|
end


;#############################################"
;       PLOTTING
;#############################################"

to ptype :n
  make "val-ptype :n
end

to points 
  checkLengths
  let [xx0 0
       yy0 0]
  dotimes [i count :xcol-data]
    [
     make "xx0 nth :i :xcol-data
     make "yy0 nth :i :ycol-data

     if(check-bounds :xx0  :yy0)
       [ if (:val-ptype = 0) [drawstring "| | :xx0 :yy0]
         if (:val-ptype = 1) [drawstring "|.| :xx0 :yy0]
         if (:val-ptype = 2) [drawstring "|o| :xx0 :yy0]
         if (:val-ptype = 3) [drawstring "|x| :xx0 :yy0]
         if (:val-ptype = 4) [drawstring "|*| :xx0 :yy0]
       ]]
end

to ltype :n
  make "val-ltype :n ; " setting the linestyle for plots
end

to connect
	checkLengths
	let [dx copylist :xcol-data]
	let [dy copylist :ycol-data]	
	dotimes[i ((count :dx) - 1)] 
	[ 
		make "xi0 nth (:i) :dx 
		make "yi0 nth (:i) :dy
		make "xi1 nth (:i + 1) :dx 
		make "yi1 nth (:i + 1) :dy
		draw-line :xi0 :yi0 :xi1 :yi1 :val-ltype
	]
end

to histogram
  	let [dx copylist :xcol-data]
  	let [dy copylist :ycol-data]
  
	dotimes[i ((count :dx) - 1)] 
   [
      make "xi0 nth (:i) :dx 
		make "yi0 nth (:i) :dy
		make "xi1 nth (:i + 1) :dx 
		make "yi1 nth (:i + 1) :dy
		make "xmid :xi0 + (:xi1 - :xi0) / 2
		draw-line :xi0 :yi0 :xmid :yi0 :val-ltype
		draw-line :xmid :yi0 :xmid :yi1 :val-ltype
		draw-line :xmid :yi1 :xi1 :yi1 :val-ltype
    ]
end

to quick-histogram
ltype 0
  let [dy copylist :ycol-data
       ycol-data copylist :dy
       name :yname
       low yminimum - 1
       high ymaximum + 1]
  bin :low :high 1
  hist :dy
  setup-screen
  limits xlower xupper ylower yupper
  box
;  grid 10 10
  xaxis 10 50 1 true
  yaxis 10 50 1 true
  histogram
  title se "|Histogram of | :name
  xlabel "|Value|
  ylabel "|Frequency|
  aspect 2 2
  display-plot tile 2 2 2 2
  make "ycol-data copylist :dy
end

to errorbars :n
  let [xx0 0
       yy0 0]
  make "xx0 nth 0 :xcol-data
  make "yy0 nth 0 :ycol-data
  moveto :xx0 :yy0
  moveto nth 0 :xcol-data nth 0 :ycol-data
  dotimes [i count :xcol-data]
    [ 
     make "xx0 nth :i :xcol-data
     make "yy0 nth :i :ycol-data
     if(check-bounds :xx0 :yy0)
       [ moveto :xx0 :yy0
         lineto :xx0  (:yy0 + :n)
         lineto :xx0 (:yy0 - :n)
       ]]
     redraw
end
 
; This word creates a least-squares fit to the selected
; data and prints the equation
to fit-line
  let [sumx 0
       sumy 0
       sumxy 0
       sumx2 0
       sumy2 0
       xmean 0
       ymean 0
       xval 0
       yval 0
       slope 0
       yint 0
       xmin xminimum
       xmax xmaximum
       ymax ymaximum]

  let [cnt (count :xcol-data)]
  dotimes [i :cnt]
    [
      make "xval nth :i :xcol-data
      make "yval nth :i :ycol-data
      make "sumx :sumx + :xval
      make "sumy :sumy + :yval
      make "sumxy :sumxy + (:xval * :yval)
      make "sumx2 :sumx2 + (:xval * :xval)
      make "sumy2 :sumy2 + (:yval * :yval)
    ]
  make "xmean :sumx / :cnt
  make "ymean :sumy / :cnt
  make "slope ((:sumxy - :sumx * :ymean) / (:sumx2 - :sumx * :xmean))
  make "yint (:ymean - :slope * :xmean) 
  moveto :xmin (:slope * :xmin + :yint)
  lineto :xmax (:slope * :xmax + :yint)
  ;shortdashedlineto :xmax (:slope * :xmax + :yint)
  moveto :xmax (:ymax - :yint)
  make "intlabel "|y-int |
  make "slabel "|slope |
  make "intlabel2 two-digit :yint
  make "slabel2 two-digit :slope
  drawstring (se "|y =| (two-digit :slope) "|x +| (two-digit :yint)) (0.5 * (xupper + xlower)) (0.2 * (yupper + ylower))
  redraw
 ; output :slope
end

to bin :n :m :p
  let [val :n]
  make "xcol-data []
  repeat (:m - :n) / :p [
    make "xcol-data se :xcol-data :val
    make "val :val + :p ]
end

to hist :n
  let [len count :xcol-data
       bin 0
       low nth 0 :xcol-data
       lendata count :n
       high nth (:len - 1) :xcol-data
       cnt 0
       val 0]

  make "ycol-data []
  repeat :len [make "ycol-data se :ycol-data 0.0]
  dotimes [i :lendata]
    [
    make "bin ((( nth :i :n) - :low) / (:high - :low)) * (:len - 1)
    make "bin round :bin
      if  and (:bin > 0) (:bin < (:len + 1)) [
        make "val  nth :bin :ycol-data
        make "val  :val + 1
        setnth :bin :ycol-data :val
      ]
    ]
end
 
to quick-plot
  setup-screen
  select-all
  limits xlower xupper ylower yupper 
  box
  xaxis 10 50 1 true
  yaxis  5 20 1 true
  ptype 2 points
  title (se insertspace :yname "| vs. | :xname)
  xlabel :xname
  ylabel :yname
end

to display-quick
 make "steadyImage False
 make "steadyPlot False
 aspect 2 2
 display-plot tile 2 2 2 2
 make "steadyImage True
 make "steadyPlot True
end

to plot-filename :name
	make "plotfn :name
end

to saveplot ; save the plot with a unique name after display-plot
     blindexec "/usr/local/i3/tools/i3-images.sh
end


to display-plot :flags
  redraw
  wait 10
  ifelse(name? "plotfn)
  [
		make "plot-name word :plotfn "|.png|
		clearname "plotfn		
  ]
  [
  		make "plot-name word :yname "|.png|
  ]
  make "doit (se "|mv | "|screen.png | :plot-name )
  blindexec :doit
  wait 10
  display-eot :flags :plot-name 
end

to lweight :npixel
   setpensize :npixel
end

to check-bounds :x :y
	if(:x < :x0) [output false]
	if(:x > :x1) [output false]
	if(:y < :y0) [output false]
	if(:y > :y1) [output false]	
	output true
end

to tickSize :ismallx :ibigx :ismally :ibigy
	make "smallx :ismallx
	make "bigx :ibigx
	make "smally :ismally
	make "bigy :ibigy
end

to xaxis :major :minor :precision :label 
	if(:label) [ xticks :major :precision ]
	major-grid-x :major
	minor-grid-x :minor
end

to yaxis :major :minor :precision :label 
	if(:label) [yticks :major :precision ]
	major-grid-y :major
	minor-grid-y :minor
end

to major-grid-x :n  
 	let [savecolor :currentcolor]
  	let [xinc (:x1 - :x0) / :n]
	let [px (:y1 - :y0) / :yp1]

	let [x :x0]
  	repeat (:x1 - :x0) / :xinc
   [
		make "x :x + :xinc
	   moveto :x (:y0) lineto :x (:y0 + (:bigx * :px))
	   moveto :x (:y1) lineto :x (:y1 - (:bigx * :px))
	]
end

to minor-grid-x :n   
 	let [savecolor :currentcolor]
	let [xinc (:x1 - :x0) / :n]
	let [px (:y1 - :y0) / :yp1]
	let [x :x0]
  	repeat (:x1 - :x0) / :xinc
   [
		make "x :x + :xinc
	   moveto :x (:y0) lineto :x (:y0 + (:smallx / 2 * :px))
	   moveto :x (:y1) lineto :x (:y1 - (:smallx / 2 * :px))
	]
end

to major-grid-y :m
  	let [yinc (:y1 - :y0) / :m]
	let [px (:y1 - :y0) / :yp1]	
	let [px (:x1 - :x0) / :xp1]
	let [y :y0]
  	repeat (:y1 - :y0) / :yinc
   [
		make "y :y + :yinc
    	moveto (:x0) :y lineto (:x0 + (:bigy  * :px)) :y
    	moveto (:x1) :y lineto (:x1 - (:bigy  * :px)) :y
	]
end

to minor-grid-y :m  
	let [yinc (:y1 - :y0) / :m]
	let [px (:x1 - :x0) / :xp1]	
	let [y :y0]
  	repeat (:y1 - :y0) / :yinc
   [
		make "y :y + :yinc
    	moveto (:x0) :y lineto (:x0 + (:smally / 2 * :px)) :y
    	moveto (:x1) :y lineto (:x1 - (:smally / 2 * :px)) :y
	]
end

to draw-line :ix1 :iy1 :ix2 :iy2 :type
	let [p1Check check-bounds :ix1 :iy1]
	let [p2Check check-bounds :ix2 :iy2]	
	ifelse(:p1Check)
	[	
    	ifelse(:p2Check)
		[
			draw-segment :ix1 :iy1 :ix2 :iy2 :type	
		]	
		[	
			let [replacementPoint find-intercept :ix1 :iy1 :ix2 :iy2]
			let [repl_x nth 0 :replacementPoint] 
			let [repl_y nth 1 :replacementPoint] 			
			draw-segment :ix1 :iy1 :repl_x :repl_y :type	
		]		
	]
	[
		ifelse(:p2Check)
		[	
			let [replacementPoint find-intercept :ix2 :iy2 :ix1 :iy1 ]
			let [repl_x nth 0 :replacementPoint] 
			let [repl_y nth 1 :replacementPoint]	
			draw-segment :repl_x :repl_y :ix2 :iy2 :type	
		]
		[
			;some day write the code for crossing the box 
		]
	]	
	
end

to find-intercept :ix1 :iy1 :ix2 :iy2 
	
	ifelse(:ix2 > :ix1) 
	[
		ifelse(:iy2 > :iy1)
		[
			;print("|x increasing, y increasing|)	
			let[ xintercept [] ]	
			let[ yintercept [] ]		
			let[yintercept (se :yintercept (y1-int :ix1 :iy1 :ix2 :iy2)) ]
	      let[yintercept (se :yintercept (:y1)) ]			
			let[xintercept (se :xintercept (:x1)) ]
			let[xintercept (se :xintercept (x1-int :ix1 :iy1 :ix2 :iy2)) ]			
		]
		[
			;print("|x increasing, y decreasing|)	
			let[ xintercept [] ]	
			let[ yintercept [] ]
			let[yintercept (se :yintercept (y0-int :ix1 :iy1 :ix2 :iy2)) ]
	      let[yintercept (se :yintercept (:y0)) ]			
			let[xintercept (se :xintercept (:x1)) ]
			let[xintercept (se :xintercept (x1-int :ix1 :iy1 :ix2 :iy2)) ]
		] 
	]
	[
		ifelse(:iy2 > :iy1)
		[
			;print("|x decreasing, y increasing|)				
			let[ xintercept [] ]	
			let[ yintercept [] ]			
			let[yintercept (se :yintercept (y1-int :ix1 :iy1 :ix2 :iy2)) ]
	      let[yintercept (se :yintercept (:y1)) ]			
			let[xintercept (se :xintercept (:x0)) ]
			let[xintercept (se :xintercept (x0-int :ix1 :iy1 :ix2 :iy2)) ]
		]
		[
			;print("|x decreasing, y decreasing|)	
			let[ xintercept [] ]	
			let[ yintercept [] ]			
			let[yintercept (se :yintercept (y0-int :ix1 :iy1 :ix2 :iy2)) ]
	      let[yintercept (se :yintercept (:y0)) ]			
			let[xintercept (se :xintercept (:x0)) ]
			let[xintercept (se :xintercept (x0-int :ix1 :iy1 :ix2 :iy2)) ]
		] 
	]	
	if(check-bounds nth 0 :yintercept nth 1 :yintercept) [output :yintercept]
	if(check-bounds nth 0 :xintercept nth 1 :xintercept) [output :xintercept]
end


to y0-int :ix1 :iy1 :ix2 :iy2 
	output XofY :ix1 :iy1 :ix2 :iy2 :y0
end

to y1-int :ix1 :iy1 :ix2 :iy2 
	output XofY :ix1 :iy1 :ix2 :iy2 :y1
end

to x0-int :ix1 :iy1 :ix2 :iy2 
	output YofX :ix1 :iy1 :ix2 :iy2 :x0
end

to x1-int :ix1 :iy1 :ix2 :iy2 
	output YofX :ix1 :iy1 :ix2 :iy2 :x1
end

to YofX :ix1 :iy1 :ix2 :iy2 :ix
	let[m (:iy2 - :iy1)/(:ix2 - :ix1)]
	output :m * (:ix - :ix1) + :iy1
end

to XofY :ix1 :iy1 :ix2 :iy2 :iy
	let[m (:iy2 - :iy1)/(:ix2 - :ix1)]
	output (:iy - :iy1) / :m + :ix1
end


to draw-segment :ix1 :iy1 :ix2 :iy2 :type
	rel :ix1 :iy1
 	if (:type = 0) [lineto :ix2 :iy2]
	if (:type = 1) [dotdashedlineto :ix2 :iy2]
	if (:type = 2) [shortdashedlineto :ix2 :iy2]
	if (:type = 3) [longdashedlineto :ix2 :iy2]
	if (:type = 4) [dotshortdashedlineto :ix2 :iy2]
	if (:type = 5) [dotlongdashedlineto :ix2 :iy2]
	if (:type = 6) [shortlongdashedlineto :ix2 :iy2]
end

to y-data-add :inList 
  init-y-series
  let [newSeries [ [] ] ]
  setnth 0 :newSeries :inList       
  make "y-series se :y-series :newSeries       
end

to init-y-series
  if not(name? "y-series)[  
    make "y-series [] 
    if(name? "ycol-data) [
      let [newSeries [ [] ] ]
      setnth 0 :newSeries :ycol-data       
      make "y-series se :y-series :newSeries
    ]
  ]
end

to multi-plot
  make "colorList [$FF0000 $00FF00 $0000FF] 
  setup-screen
  select-all
  combineLists
  limits xlower xupper MinEntry :alldata MaxEntry :alldata 
  box
  xaxis 10 50 1 true
  yaxis 5 20 1 true
  ptype 2
  title (se insertspace :yname "| vs. | :xname)
  xlabel :xname
  ylabel :yname
  dotimes [i count :y-series]
  [
    let [ycol-data nth :i :y-series]    
    color nth :i :colorList 
    points               
  ]
end

to combineLists
  make "alldata []
  dotimes [i count :y-series]
  [
    let [data nth :i :y-series]
    make "alldata (se :alldata :data) 
  ]
end

to checkLengths
	if((count :xcol-data) > (count :ycol-data))[
		make "xcol-data bl :xcol-data
		checkLengths

	]
end


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; Evaporation Version ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;this is our primary point of entry into the code
to run-once
	make "playback false   
	init ;set the experiment into a state where it is running and collecting data
	loop [ ;main program loop
		if (elapsedTime > ( 60 )) [ stop ] ;run this for 600 seconds and the stop.  
		receive-packet ;waits until a packet is available, and then returns it so we can process it    
		wait 10
	]
end

;initializes both jLogo and uLogo and start the experiment running
to init
	make "type 99
	print (se "Starting  :project-name) ;print an init message
	init-jlogo ;initialize jLogo
end

;makes the needed calibration lists
to init-calibration      
    make "temp0_cal [112 0 213 54] ;the calibration list for an average of 10 readings from tempsensor1
    make "temp1_cal [112 0 213 54] ;the calibration list for an average of 10 readings from tempsensor1


;handles a packet after it has been received
to process-data-packet
   	if ((:type = 99) = false) [ stop ] ;check to make sure we have the right packet type, if not, stop. 
   	calibrate-list "temp0-list :p99-word00 :temp0_cal ;calibrate list of ADU into list of Deg C
   	calibrate-list "temp1-list :p99-word01 :temp1_cal ;calibrate list of ADU into list of Deg C  	
	make "temp-first first :temp0-list ;create variable to hold the very first reading from temp0	
	make "temp-second first :temp1-list ;create variable to hold the very first reading from temp1		
	;calc-min ;find the minimum value of temperature
	;plot-temp0 ;create the plot of temperature vs time		
	;plot-temp1 ;create the plot of temperature vs time				
end

;find the minimum value of temperature
to calc-min
	make "temp-min MinEntry :temp0-list ;find the minimum value of temperature
	let [temp-index itempos :temp-min :temp0-list] ;find its position in the list
	make "temp-time item :temp-index :time-list ;find the corresponding value of time
end

; new example of plot-temp2
to plot-temp0
   plot-title "|Temp 0|                     ; every plot must start with a title
   top-plot                                 ; one plot on top right
                                            ; other options are one-plot and bottom-plot
   x-data "|Elapsed_Time_(sec)| :time-list  ; xdata is time
   y-data "|Temp_0_(deg_C)|     :temp0-list ; ydata is temp2
   select-all                               ; select all times
   limits xlower xupper ylower yupper       ; auto scale both x and y axis
   x-tick 10 5                              ; 10 major ticks 5 minor ticks per major tick
   y-tick  5 4

	show-plot
                              ; 10 major ticks 4 minor ticks per major tick                            ; every plot must end with display-plot
end

to plot-temp1
   plot-title "|Temp 1|                     ; every plot must start with a title
   bottom-plot                                 ; one plot on top right
                                            ; other options are one-plot and bottom-plot
   x-data "|Elapsed_Time_(sec)| :time-list  ; xdata is time
   y-data "|Temp_1_(deg_C)|     :temp1-list ; ydata is temp2
   select-all                               ; select all times
   limits xlower xupper ylower yupper       ; auto scale both x and y axis
   x-tick 10 5                              ; 10 major ticks 5 minor ticks per major tick
   y-tick  5 4

	show-plot
                              ; 10 major ticks 4 minor ticks per major tick
end

;test